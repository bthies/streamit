/*
 * StreamItParserFE.g: StreamIt parser producing front-end tree
 * $Id: StreamItParserFE.g,v 1.9 2002-09-30 21:25:31 dmaze Exp $
 */

header {
	package streamit.frontend;

	import streamit.frontend.nodes.*;

	import java.util.Collections;
	import java.io.DataInputStream;
	import java.util.List;

	import java.util.ArrayList;
}

class StreamItParserFE extends Parser;
options {
	importVocab=StreamItLex;	// use vocab generated by lexer
}

{
	public static void main(String[] args)
	{
		try
		{
			DataInputStream dis = new DataInputStream(System.in);
			StreamItLex lexer = new StreamItLex(dis);
			StreamItParserFE parser = new StreamItParserFE(lexer);
			parser.program();
		}
		catch (Exception e)
		{
			e.printStackTrace(System.err);
		}
	}

	public FEContext getContext(Token t)
	{
		int line = t.getLine();
		if (line == 0) line = -1;
		int col = t.getColumn();
		if (col == 0) col = -1;
		return new FEContext(null, line, col);
	}
}

program	returns [Program p]
{ p = null; List structs = new ArrayList(); List streams = new ArrayList();
	TypeStruct ts; StreamSpec ss; }
	:	( ts=struct_decl { structs.add(ts); }
		| ss=stream_decl { streams.add(ss); }
		)*
		EOF
		// Can get away with no context here.
		{ p = new Program(null, streams, structs); }
	;

stream_decl returns [StreamSpec ss] { ss = null; StreamType st; }
	:	st=stream_type_decl
		(ss=filter_decl[st] | ss=struct_stream_decl[st])
	;

filter_decl[StreamType st] returns [StreamSpec ss]
{ ss = null; List params = Collections.EMPTY_LIST;
	List vars = new ArrayList(); List funcs = new ArrayList();
	Function fn; Statement decl; }
	:	TK_filter
		id:ID
		(params=param_decl_list)?
		LCURLY
		( fn=init_decl { funcs.add(fn); }
		| fn=work_decl { funcs.add(fn); }
		| (function_decl) => fn=function_decl { funcs.add(fn); }
		| decl=variable_decl SEMI { vars.add(decl); }
		)*
		RCURLY
		{ ss = new StreamSpec(st.getContext(), StreamSpec.STREAM_FILTER,
				st, id.getText(), params, vars, funcs); }
	;

stream_type_decl returns [StreamType st] { st = null; Type in, out; }
	:	in=data_type t:ARROW out=data_type
		// Again, want context from the input type, but Types aren't
		// FENodes.
		{ st = new StreamType(getContext(t), in, out); }
	;

struct_stream_decl[StreamType st] returns [StreamSpec ss]
{ ss = null; int type = 0;
	List params = Collections.EMPTY_LIST; Statement body; }
	:	( TK_pipeline { type = StreamSpec.STREAM_PIPELINE; }
		| TK_splitjoin { type = StreamSpec.STREAM_SPLITJOIN; }
		| TK_feedbackloop { type = StreamSpec.STREAM_FEEDBACKLOOP; }
		)
		id:ID
		(params=param_decl_list)?
		body=block
		{ ss = new StreamSpec(st.getContext(), type, st, id.getText(),
				params, body); }
	;

work_decl returns [FuncWork f]
{	f = null;
	Expression pop = null, peek = null, push = null;
	Statement s;
}
	:	t:TK_work
		(	TK_push push=right_expr
		|	TK_pop pop=right_expr
		|	TK_peek peek=right_expr
		)*
		s=block
		{ f = new FuncWork(getContext(t), null, s, peek, pop, push); }
	;

init_decl returns [Function f] { Statement s; f = null; }
	:	t:TK_init s=block { f = Function.newInit(getContext(t), s); }
	;

push_statement returns [Statement s] { s = null; Expression x; }
	:	t:TK_push LPAREN x=right_expr RPAREN
		{ s = new StmtPush(getContext(t), x); }
	;

statement returns [Statement s] { s = null; }
	:	s=streamit_statement
	;

streamit_statement returns [Statement s] { s = null; }
	:	s=minic_statement
	|	s=add_statement SEMI
	|	s=body_statement SEMI
	| 	s=loop_statement SEMI
	|	s=split_statement SEMI
	|	s=join_statement SEMI
	|	s=enqueue_statement SEMI
	|	s=push_statement SEMI
	;

add_statement returns [Statement s] { s = null; StreamCreator sc; }
	: t:TK_add sc=stream_or_inline { s = new StmtAdd(getContext(t), sc); }
	;

body_statement returns [Statement s] { s = null; StreamCreator sc; }
	: t:TK_body sc=stream_or_inline { s = new StmtBody(getContext(t), sc); }
	;

loop_statement returns [Statement s] { s = null; StreamCreator sc; }
	: t:TK_loop sc=stream_or_inline { s = new StmtLoop(getContext(t), sc); }
	;

stream_or_inline returns [StreamCreator sc]
{ sc = null; StreamType st; List params = new ArrayList(); Statement body;
List types = new ArrayList(); Type t; }
	: (stream_type_decl TK_filter) =>
		st=stream_type_decl tf:TK_filter body=block
		// (Is this even right?  What's the syntax for inline filter decls?)
	| tp:TK_pipeline body=block
		{ sc = new SCAnon(getContext(tp), StreamSpec.STREAM_PIPELINE, body); }
	| ts:TK_splitjoin body=block
		{ sc = new SCAnon(getContext(ts), StreamSpec.STREAM_SPLITJOIN, body); }
	| tl:TK_feedbackloop body=block
		{ sc = new SCAnon(getContext(tl), StreamSpec.STREAM_FEEDBACKLOOP, body); }
	| id:ID
		(LESS_THAN t=data_type MORE_THAN { types.add(t); })?
		(params=func_call_params)?
		{ sc = new SCSimple(getContext(id), id.getText(), types, params); }
	;

split_statement returns [Statement s] { s = null; SplitterJoiner sj; }
	: t:TK_split sj=splitter_or_joiner
		{ s = new StmtSplit(getContext(t), sj); }
	;

join_statement returns [Statement s] { s = null; SplitterJoiner sj; }
	: t:TK_join sj=splitter_or_joiner
		{ s = new StmtJoin(getContext(t), sj); }
	;

splitter_or_joiner returns [SplitterJoiner sj]
{ sj = null; Expression x; List l; }
	: tr:TK_roundrobin
		( (LPAREN RPAREN) => LPAREN RPAREN
			{ sj = new SJRoundRobin(getContext(tr)); }
		| (LPAREN right_expr RPAREN) => LPAREN x=right_expr RPAREN
			{ sj = new SJRoundRobin(getContext(tr), x); }
		| l=func_call_params { sj = new SJWeightedRR(getContext(tr), l); }
		| { sj = new SJRoundRobin(getContext(tr)); }
		)
	| td:TK_duplicate (LPAREN RPAREN)?
		{ sj = new SJDuplicate(getContext(td)); }
	;

enqueue_statement returns [Statement s] { s = null; Expression x; }
	: t:TK_enqueue x=right_expr { s = new StmtEnqueue(getContext(t), x); }
	;

data_type returns [Type t] { t = null; Expression x; }
	:	t=primitive_type
		(LSQUARE x=right_expr RSQUARE
			{ t = new TypeArray(t, x); }
		)*
	|	TK_void { t = new TypePrimitive(TypePrimitive.TYPE_VOID); }
	;

primitive_type returns [Type t] { t = null; }
	:	TK_bit { t = new TypePrimitive(TypePrimitive.TYPE_BIT); }
	|	TK_int { t = new TypePrimitive(TypePrimitive.TYPE_INT); }
	|	TK_float { t = new TypePrimitive(TypePrimitive.TYPE_FLOAT); }
	|	TK_double { t =  new TypePrimitive(TypePrimitive.TYPE_DOUBLE); }
	|	TK_complex { t = new TypePrimitive(TypePrimitive.TYPE_COMPLEX); }
	|	id:ID { t = new TypeStructRef(id.getText()); }
	;

variable_decl returns [Statement s] { s = null; Type t; Expression x = null; }
	:	t=data_type id:ID (ASSIGN x=right_expr)?
		{ s = new StmtVarDecl(getContext(id), t, id.getText(), x); }
		// NB: we'd use the context of t, except Type doesn't include
		// a context.  This is probably okay in the grand scheme of things.
	;

function_decl returns [Function f] { Type t; List l; Statement s; f = null; }
	:	t=data_type id:ID^
		l=param_decl_list
		s=block
	;

param_decl_list returns [List l] { l = new ArrayList(); Parameter p; }
	:	LPAREN
		(p=param_decl { l.add(p); } (COMMA p=param_decl { l.add(p); })*
		)?
		RPAREN
	;

param_decl returns [Parameter p] { Type t; p = null; }
	:	t=data_type id:ID { p = new Parameter(t, id.getText()); }
	;

block returns [Statement s] { s = null; List l = new ArrayList(); }
	:	t:LCURLY ( s=statement { l.add(s); } )* RCURLY
		{ s = new StmtBlock(getContext(t), l); }
	;

minic_statement returns [Statement s]
{ s = null; Statement s1, s2; Expression x; }
	:	s=block
	|	(variable_decl) => s=variable_decl SEMI!
	|	(expr_statement) => s=expr_statement SEMI!
	|	tb:TK_break SEMI { s = new StmtBreak(getContext(tb)); }
	|	tc:TK_continue SEMI { s = new StmtContinue(getContext(tc)); }
	|	s=return_statement SEMI
	|	s=if_else_statement
	|	s=while_statement
	|	s=do_while_statement SEMI
	|	s=for_statement
	;

return_statement returns [Statement s] { s = null; Expression x = null; }
	:	t:TK_return (x=right_expr)? { s = new StmtReturn(getContext(t), x); }
	;

if_else_statement returns [Statement s]
{ s = null; Expression x; Statement t, f = null; }
	:	u:TK_if LPAREN x=right_expr RPAREN t=statement
		((TK_else) => (TK_else f=statement))?
		{ s = new StmtIfThen(getContext(u), x, t, f); }
	;

while_statement returns [Statement s] { s = null; Expression x; Statement b; }
	:	t:TK_while LPAREN x=right_expr RPAREN b=statement
		{ s = new StmtWhile(getContext(t), x, b); }
	;

do_while_statement returns [Statement s]
{ s = null; Expression x; Statement b; }
	:	t:TK_do b=statement TK_while LPAREN x=right_expr RPAREN
		{ s = new StmtDoWhile(getContext(t), b, x); }
	;

for_statement returns [Statement s]
{ s = null; Expression x; Statement a, b, c; }
	:	t:TK_for LPAREN a=for_init_statement SEMI x=right_expr SEMI
		b=for_incr_statement RPAREN c=statement
		{ s = new StmtFor(getContext(t), a, x, b, c); }
	;

for_init_statement returns [Statement s] { s = null; }
	:	(variable_decl) => s=variable_decl
	|	(expr_statement) => s=expr_statement
	;

for_incr_statement returns [Statement s] { s = null; }
	:	s=expr_statement
	;

expr_statement returns [Statement s] { s = null; Expression x; }
	:	(incOrDec) => x=incOrDec { s = new StmtExpr(x); }
	|	(assign_expr) => s=assign_expr
	|	(func_call) => x=func_call { s = new StmtExpr(x); }
	|	x=streamit_value_expr { s = new StmtExpr(x); }
	;

assign_expr returns [Statement s] { s = null; Expression l, r; int o = 0; }
	:	l=left_expr
		(	ASSIGN { o = 0; }
		|	PLUS_EQUALS { o = ExprBinary.BINOP_ADD; }
		| 	MINUS_EQUALS { o = ExprBinary.BINOP_SUB; }
		|	STAR_EQUALS { o = ExprBinary.BINOP_MUL; }
		|	DIV_EQUALS { o = ExprBinary.BINOP_DIV; }
		)
		r=right_expr
		{ s = new StmtAssign(l.getContext(), l, r, o); }
	;

func_call returns [Expression x] { x = null; List l; }
	:	name:ID l=func_call_params
		{ x = new ExprFunCall(getContext(name), name.getText(), l); }
	;

func_call_params returns [List l] { l = new ArrayList(); Expression x; }
	:	LPAREN
		(	x=right_expr { l.add(x); }
			(COMMA x=right_expr { l.add(x); })*
		)?
		RPAREN
	;

left_expr returns [Expression x] { x = null; }
	:	x=value
	;

right_expr returns [Expression x] { x = null; }
	:	x=ternaryExpr
	;

ternaryExpr returns [Expression x] { x = null; Expression b, c; }
	:	x=logicOrExpr
		(QUESTION b=ternaryExpr COLON c=ternaryExpr
			{ x = new ExprTernary(x.getContext(), ExprTernary.TEROP_COND,
					x, b, c); }
		)?
	;

logicOrExpr returns [Expression x] { x = null; Expression r; int o = 0; }
	:	x=logicAndExpr
		(LOGIC_OR r=logicOrExpr
			{ x = new ExprBinary(x.getContext(), ExprBinary.BINOP_OR, x, r); }
		)?
	;

logicAndExpr returns [Expression x] { x = null; Expression r; }
	:	x=bitwiseExpr
		(LOGIC_AND r=logicAndExpr
			{ x = new ExprBinary(x.getContext(), ExprBinary.BINOP_AND, x, r); }
		)?
	;

bitwiseExpr returns [Expression x] { x = null; Expression r; int o = 0; }
	:	x=equalExpr
		(	( BITWISE_OR  { o = ExprBinary.BINOP_BOR; }
			| BITWISE_AND { o = ExprBinary.BINOP_BAND; }
			| BITWISE_XOR { o = ExprBinary.BINOP_BXOR; }
			)
			r=bitwiseExpr
			{ x = new ExprBinary(x.getContext(), o, x, r); }
		)?
	;

equalExpr returns [Expression x] { x = null; Expression r; int o = 0; }
	:	x=compareExpr
		(	( EQUAL     { o = ExprBinary.BINOP_EQ; }
			| NOT_EQUAL { o = ExprBinary.BINOP_NEQ; }
			)
			r = equalExpr
			{ x = new ExprBinary(x.getContext(), o, x, r); }
		)?
	;

compareExpr returns [Expression x] { x = null; Expression r; int o = 0; }
	:	x=addExpr
		(	( LESS_THAN  { o = ExprBinary.BINOP_LT; }
			| LESS_EQUAL { o = ExprBinary.BINOP_LE; }
			| MORE_THAN  { o = ExprBinary.BINOP_GT; }
			| MORE_EQUAL { o = ExprBinary.BINOP_GE; }
			)
			r = compareExpr
			{ x = new ExprBinary(x.getContext(), o, x, r); }
		)?
	;

addExpr returns [Expression x] { x = null; Expression r; int o = 0; }
	:	x=multExpr
		(	( PLUS  { o = ExprBinary.BINOP_ADD; }
			| MINUS { o = ExprBinary.BINOP_SUB; }
			)
			r=addExpr
			{ x = new ExprBinary(x.getContext(), o, x, r); }
		)?
	;

multExpr returns [Expression x] { x = null; Expression r; int o = 0; }
	:	x=inc_dec_expr
		(	( STAR { o = ExprBinary.BINOP_MUL; }
			| DIV  { o = ExprBinary.BINOP_DIV; }
			| MOD  { o = ExprBinary.BINOP_MOD; }
			)
			r=multExpr
			{ x = new ExprBinary(x.getContext(), o, x, r); }
		)?
	;

inc_dec_expr returns [Expression x] { x = null; }
	:	(incOrDec) => x=incOrDec
	|	x=value_expr
	;

incOrDec returns [Expression x] { x = null; }
	:	x=left_expr
		(	INCREMENT
			{ x = new ExprUnary(x.getContext(), ExprUnary.UNOP_POSTINC, x); }
		|	DECREMENT
			{ x = new ExprUnary(x.getContext(), ExprUnary.UNOP_POSTDEC, x); }
		)
	|	i:INCREMENT x=left_expr
			{ x = new ExprUnary(getContext(i), ExprUnary.UNOP_PREINC, x); }
	|	d:DECREMENT x=left_expr
			{ x = new ExprUnary(getContext(d), ExprUnary.UNOP_PREDEC, x); }
	;

value_expr returns [Expression x] { x = null; }
	:	x=minic_value_expr
	|	x=streamit_value_expr
	;

streamit_value_expr returns [Expression x] { x = null; }
	:	t:TK_pop LPAREN RPAREN
			{ x = new ExprPop(getContext(t)); }
	|	u:TK_peek LPAREN x=right_expr RPAREN
			{ x = new ExprPeek(getContext(u), x); }
	;

minic_value_expr returns [Expression x] { x = null; }
	:	LPAREN x=right_expr RPAREN
	|	(func_call) => x=func_call
	|	x=value
	|	x=constantExpr
	;

value returns [Expression x] { x = null; Expression array; List l; }
	:	name:ID { x = new ExprVar(getContext(name), name.getText()); }
		(	DOT field:ID
			{ x = new ExprField(x.getContext(), x, field.getText()); }
		|	LSQUARE array=right_expr RSQUARE
			{ x = new ExprArray(x.getContext(), x, array); }
		)*
	;

constantExpr returns [Expression x] { x = null; }
	:	n:NUMBER
			{ x = ExprConstant.createConstant(getContext(n), n.getText()); }
	|	c:CHAR_LITERAL
			{ x = new ExprConstChar(getContext(c), c.getText()); }
	|	s:STRING_LITERAL
			{ x = new ExprConstStr(getContext(s), s.getText()); }
	|	pi:TK_pi
			{ x = new ExprConstFloat(getContext(pi), Math.PI); }
	;

struct_decl returns [TypeStruct ts]
{ ts = null; Parameter p; List names = new ArrayList();
	List types = new ArrayList(); }
	:	t:TK_struct id:ID
		LCURLY
		(p=param_decl SEMI
			{ names.add(p.getName()); types.add(p.getType()); }
		)*
		RCURLY
		{ ts = new TypeStruct(id.getText(), names, types); }
		// NB: this throws away context information, so the best we
		// can do when reporting errors is to refer to things by name.
		// Might change TypeStruct to work around this.
	;
