dnl
dnl configure.in: StreamIt configuration script
dnl Process this file with 'autoconf' to produce a configure script.
dnl $Id: configure.in,v 1.8 2003-10-01 18:11:47 dmaze Exp $
dnl

AC_INIT([StreamIt], [2.0], [streamit@cag.lcs.mit.edu])

dnl which release is this?
AC_MSG_CHECKING([for source release])
if test -d $srcdir/src; then
  STREAMIT_SRC=1
  AC_MSG_RESULT([yes])
else
  STREAMIT_SRC=0
  AC_MSG_RESULT([no])
fi

dnl checks for programs
dnl These are essential, without them nothing goes.
AC_PATH_PROG(PERL, perl)
if test -z "$PERL"; then
AC_MSG_ERROR(
[Could not find perl.  This is needed to run the main strc compiler script;
please install perl on your system and re-run configure.])
fi

AC_CHECK_PROGS(JAVA, [java])dnl can add more JVMs here
if test -z "$JAVA"; then
AC_MSG_ERROR(
[Could not find a Java virtual machine.  This is needed to run the
compiler.  If your JVM is not named 'java', please re-run configure,
adding JAVA=jvmname to the end of the command line.])
fi

if test "$STREAMIT_SRC" = 1; then
AC_CHECK_PROGS(JIKES, [jikes])dnl only; used for compilation
if test -z "$JIKES"; then
AC_MSG_ERROR(
[Could not find the Jikes Java compiler.  The StreamIt compiler's
build system uses Jikes (and only jikes) to compile the compiler.
Please install Jikes (http://www.jikes.org/) and re-run configure.])
fi
fi # STREAMIT_SRC

AC_CHECK_PROGS(JAVAC, [jikes javac])
if test -z "$JAVAC"; then
AC_MSG_WARN(
[Could not find a Java compiler.  The StreamIt compiler should still
run fine, but you may not be able to test programs separately under
the StreamIt Java runtime library.])
fi

dnl This needs an install-sh:
dnl AC_PROG_INSTALL
AC_PROG_LN_S

dnl Uniprocessor bits:
AC_PROG_RANLIB
AC_PROG_CC
AC_CHECK_TOOL(AR, ar)
if test -z "$RANLIB" || test -z "$CC" || test -z "$AR"; then
AC_MSG_WARN(
[Some components of your C compiler's toolchain are missing.
Ths StreamIt compiler should still run fine, but you may not be
able to use the output of the normal uniprocessor backend.])
fi

AC_PATH_PROG(PYTHON, python)dnl what uses this?

if test "$STREAMIT_SRC" = 1; then
dnl Documentation bits:
CAN_DOC=1
AC_CHECK_PROGS(LATEX, latex)
AC_CHECK_PROGS(BIBTEX, bibtex)
AC_CHECK_PROGS(DVIPS, dvips)
AC_CHECK_PROGS(MPOST, mpost)
if test -z "$LATEX" || test -z "$BIBTEX" || test -z "$DVIPS" || \
  test -z "$MPOST"; then
CAN_DOC=0
AC_MSG_WARN(
[You don't seem to have a complete LaTeX installation.
Rebuilding the documentation from source won't work.])
else
AC_CHECK_PROGS(PS2PDF, ps2pdf)
if test -z "$PS2PDF"; then
CAN_DOC=0
AC_MSG_WARN(
[You don't have a ps2pdf, so you can't produce PDF
versions of the documentation.  Because of this, rebuilding the
documentation from source won't work.])
fi
AC_CHECK_PROGS(HEVEA, hevea)
AC_CHECK_PROGS(IMAGEN, imagen)
if test -z "$HEVEA" || test -z "$IMAGEN"; then
CAN_DOC=0
AC_MSG_WARN(
[You don't seem to have the hevea LaTeX-to-HTML converter,
so you can't produce HTML or text versions of the documentation.  Because
of this, rebuilding the documentation from source won't work.])
fi
fi

dnl ANTLR:
AC_MSG_CHECKING([for antlr.Tool])

if test "$STREAMIT_SRC" = 1; then
case `$JAVA antlr.Tool 2>&1` in
*java.lang.NoClassDefFoundError:*)
  AC_MSG_RESULT([no])
  AC_MSG_ERROR(
[Couldn't find the antlr.Tool class in your Java CLASSPATH.  If you have
ANTLR installed somewhere, add the antlr.jar file from the distribution
to your CLASSPATH and re-run configure.  Otherwise, get ANTLR from
http://www.antlr.org/.])
  ;;
*)
  AC_MSG_RESULT([yes])
  ;;
esac
fi
fi

dnl checks for libraries
HAVE_FFTW=1
AC_SUBST([HAVE_FFTW])
AC_CHECK_LIB(sfftw, fftw_create_plan, :, [HAVE_FFTW=0], [-lm])
AC_CHECK_LIB(srfftw, rfftw_create_plan, :, [HAVE_FFTW=0], [-lm -lsfftw])

dnl checks for header files
AC_CHECK_HEADER(sfftw.h, :, [HAVE_FFTW=0])
AC_CHECK_HEADER(srfftw.h, :, [HAVE_FFTW=0])

if test "$HAVE_FFTW" = 0; then
AC_MSG_WARN(
[You don't seem to have the fftw library installed, or maybe
it's not built properly for single-precision floats.  The uniprocessor
C library will be built to not use fftw for Fourier transforms, but this
means that the frequency replacement option will fail.])
fi

dnl checks for types
dnl checks for structures
dnl checks for compiler characteristics
dnl checks for library functions
dnl checks for system services

dnl disable building things that only work here
CAG_BUILD=0
AC_SUBST([CAG_BUILD])

AC_CONFIG_FILES([Makefile library/c/Makefile misc/Makefile.vars])
AC_CONFIG_FILES([strc], [chmod +x strc])
AC_OUTPUT
