// Define globals, and sample user keys
// (rodric rabbah, <rabbah@mit.edu>)

static {
    // used for printing descriptor in output
    boolean PRINTINFO  = false;
    int PLAINTEXT      = 0;
    int USERKEY        = 1;
    int CIPHERTEXT     = 2;

    // algorithm has 16 total rounds
    int MAXROUNDS      = 4;

    // sample user keys
    int[34][2] USERKEYS = {{0x00000000, 0x00000000},  // 0x0000000000000000
                           {0xFFFFFFFF, 0xFFFFFFFF},  // 0xFFFFFFFFFFFFFFFF
                           {0x30000000, 0x00000000},  // 0x3000000000000000
                           {0x11111111, 0x11111111},  // 0x1111111111111111
                           {0x01234567, 0x89ABCDEF},  // 0x0123456789ABCDEF
                           {0x11111111, 0x11111111},  // 0x1111111111111111
                           {0x00000000, 0x00000000},  // 0x0000000000000000
                           {0xFEDCBA98, 0x76543210},  // 0xFEDCBA9876543210
                           {0x7CA11045, 0x4A1A6E57},  // 0x7CA110454A1A6E57
                           {0x0131D961, 0x9DC1376E},  // 0x0131D9619DC1376E
                           {0x07A1133E, 0x4A0B2686},  // 0x07A1133E4A0B2686
                           {0x3849674C, 0x2602319E},  // 0x3849674C2602319E
                           {0x04B915BA, 0x43FEB5B6},  // 0x04B915BA43FEB5B6
                           {0x0113B970, 0xFD34F2CE},  // 0x0113B970FD34F2CE
                           {0x0170F175, 0x468FB5E6},  // 0x0170F175468FB5E6
                           {0x43297FAD, 0x38E373FE},  // 0x43297FAD38E373FE
                           {0x07A71370, 0x45DA2A16},  // 0x07A7137045DA2A16
                           {0x04689104, 0xC2FD3B2F},  // 0x04689104C2FD3B2F
                           {0x37D06BB5, 0x16CB7546},  // 0x37D06BB516CB7546
                           {0x1F08260D, 0x1AC2465E},  // 0x1F08260D1AC2465E
                           {0x58402364, 0x1ABA6176},  // 0x584023641ABA6176
                           {0x02581616, 0x4629B007},  // 0x025816164629B007
                           {0x49793EBC, 0x79B3258F},  // 0x49793EBC79B3258F
                           {0x4FB05E15, 0x15AB73A7},  // 0x4FB05E1515AB73A7
                           {0x49E95D6D, 0x4CA229BF},  // 0x49E95D6D4CA229BF
                           {0x018310DC, 0x409B26D6},  // 0x018310DC409B26D6
                           {0x1C587F1C, 0x13924FEF},  // 0x1C587F1C13924FEF
                           {0x01010101, 0x01010101},  // 0x0101010101010101
                           {0x1F1F1F1F, 0x0E0E0E0E},  // 0x1F1F1F1F0E0E0E0E
                           {0xE0FEE0FE, 0xF1FEF1FE},  // 0xE0FEE0FEF1FEF1FE
                           {0x00000000, 0x00000000},  // 0x0000000000000000
                           {0xFFFFFFFF, 0xFFFFFFFF},  // 0xFFFFFFFFFFFFFFFF
                           {0x01234567, 0x89ABCDEF},  // 0x0123456789ABCDEF
                           {0xFEDCBA98, 0x76543210}}; // 0xFEDCBA9876543210

    // PC1 permutation for key schedule
    int[56] PC1 = {57, 49, 41, 33, 25, 17,  9,
                    1, 58, 50, 42, 34, 26, 18,
                   10,  2, 59, 51, 43, 35, 27,
                   19, 11,  3, 60, 52, 44, 36,
                   63, 55, 47, 39, 31, 23, 15,
                    7, 62, 54, 46, 38, 30, 22,
                   14,  6, 61, 53, 45, 37, 29,
                   21, 13,  5, 28, 20, 12,  4};
    
    // PC2 permutation for key schedule
    int[48] PC2 = {14, 17, 11, 24,  1,  5,
                    3, 28, 15,  6, 21, 10,
                   23, 19, 12,  4, 26,  8,
                   16,  7, 27, 20, 13,  2,
                   41, 52, 31, 37, 47, 55,
                   30, 40, 51, 45, 33, 48,
                   44, 49, 39, 56, 34, 53,
                   46, 42, 50, 36, 29, 32};
    
    // key rotation table for key schedule
    int[16] RT = {1, 1, 2, 2, 
                  2, 2, 2, 2, 
                  1, 2, 2, 2, 
                  2, 2, 2, 1};
    
    // initial permuation
    int[64] IP = {58, 50, 42, 34, 26, 18, 10, 2,
                  60, 52, 44, 36, 28, 20, 12, 4,
                  62, 54, 46, 38, 30, 22, 14, 6,
                  64, 56, 48, 40, 32, 24, 16, 8,
                  57, 49, 41, 33, 25, 17,  9, 1,
                  59, 51, 43, 35, 27, 19, 11, 3,
                  61, 53, 45, 37, 29, 21, 13, 5,
                  63, 55, 47, 39, 31, 23, 15, 7};

    // expansion permutation (bit selection)
    int[48] E = {32,  1,  2,  3,  4,  5,
                  4,  5,  6,  7,  8,  9,
                  8,  9, 10, 11, 12, 13,
                 12, 13, 14, 15, 16, 17,
                 16, 17, 18, 19, 20, 21,
                 20, 21, 22, 23, 24, 25,
                 24, 25, 26, 27, 28, 29,
                 28, 29, 30, 31, 32,  1};
    
    // P permutation of sbox output
    int[32] P = {16,  7, 20, 21,
                 29, 12, 28, 17,
                  1, 15, 23, 26,
                  5, 18, 31, 10,
                  2,  8, 24, 14,
                 32, 27,  3,  9,
                 19, 13, 30,  6,
                 22, 11,  4, 25};
    
    // inverse intial permuation
    int[64] IPm1 = {40, 8, 48, 16, 56, 24, 64, 32,
                    39, 7, 47, 15, 55, 23, 63, 31,
                    38, 6, 46, 14, 54, 22, 62, 30,
                    37, 5, 45, 13, 53, 21, 61, 29,
                    36, 4, 44, 12, 52, 20, 60, 28,
                    35, 3, 43, 11, 51, 19, 59, 27,
                    34, 2, 42, 10, 50, 18, 58, 26,
                    33, 1, 41,  9, 49, 17, 57, 25};
    
    // provides sbox permutations for DES encryption
    int[4][16] S1 = {{14,  4, 13,  1,  2, 15, 11,  8,  3, 10,  6, 12,  5,  9,  0,  7},
                     { 0, 15,  7,  4, 14,  2, 13,  1, 10,  6, 12, 11,  9,  5,  3,  8},
                     { 4,  1, 14,  8, 13,  6,  2, 11, 15, 12,  9,  7,  3, 10,  5,  0},
                     {15, 12,  8,  2,  4,  9,  1,  7,  5, 11,  3, 14, 10,  0,  6, 13}};
    
    int[4][16] S2 = {{15,  1,  8, 14,  6, 11,  3,  4,  9,  7,  2, 13, 12,  0,  5, 10},
                     { 3, 13,  4,  7, 15,  2,  8, 14, 12,  0,  1, 10,  6,  9, 11,  5},
                     { 0, 14,  7, 11, 10,  4, 13,  1,  5,  8, 12,  6,  9,  3,  2, 15},
                     {13,  8, 10,  1,  3, 15,  4,  2, 11,  6,  7, 12,  0,  5, 14, 9}};
    
    int[4][16] S3 = {{10,  0,  9, 14,  6,  3, 15,  5,  1, 13, 12,  7, 11,  4,  2,  8},
                     {13 , 7,  0,  9,  3,  4,  6, 10,  2,  8,  5, 14, 12, 11, 15,  1},
                     {13 , 6,  4,  9,  8, 15,  3,  0, 11,  1,  2, 12,  5, 10, 14,  7},
                     { 1, 10, 13,  0,  6,  9,  8,  7,  4, 15, 14,  3, 11,  5,  2, 12}};
    
    int[4][16] S4 = {{ 7, 13, 14,  3,  0,  6,  9, 10,  1,  2,  8,  5, 11, 12,  4, 15},
                     {13,  8, 11,  5,  6, 15,  0,  3,  4,  7,  2, 12,  1, 10, 14,  9},
                     {10,  6,  9,  0, 12, 11,  7, 13, 15,  1,  3, 14,  5,  2,  8,  4},
                     { 3, 15,  0,  6, 10,  1, 13,  8,  9,  4,  5, 11, 12,  7,  2, 14}};
    
    int[4][16] S5 = {{ 2, 12,  4,  1,  7, 10, 11,  6,  8,  5,  3, 15, 13,  0, 14,  9},
                     {14, 11,  2, 12,  4,  7, 13,  1,  5,  0, 15, 10,  3,  9,  8,  6},
                     { 4,  2,  1, 11, 10, 13,  7,  8, 15,  9, 12,  5,  6,  3,  0, 14},
                     {11,  8, 12,  7,  1, 14,  2, 13,  6, 15,  0,  9, 10,  4,  5,  3}}; 
    
    int[4][16] S6 = {{12,  1, 10, 15,  9,  2,  6,  8,  0, 13,  3,  4, 14,  7,  5, 11},
                     {10, 15,  4,  2,  7, 12,  9,  5,  6,  1, 13, 14,  0, 11,  3,  8},
                     { 9, 14, 15,  5,  2,  8, 12,  3,  7,  0,  4, 10,  1, 13, 11,  6},
                     { 4,  3,  2, 12,  9,  5, 15, 10, 11, 14,  1,  7,  6,  0,  8, 13}};
    
    int[4][16] S7 = {{ 4, 11,  2, 14, 15,  0,  8, 13,  3, 12,  9,  7,  5, 10,  6,  1},
                     {13,  0, 11,  7,  4,  9,  1, 10, 14,  3,  5, 12,  2, 15,  8,  6},
                     { 1,  4, 11, 13, 12,  3,  7, 14, 10, 15,  6,  8,  0,  5,  9,  2},
                     { 6, 11, 13,  8,  1,  4, 10,  7,  9,  5,  0, 15, 14,  2,  3, 12}};
    
    int[4][16] S8 = {{13,  2,  8,  4,  6, 15, 11,  1, 10,  9,  3, 14,  5,  0, 12,  7},
                     { 1, 15, 13,  8, 10,  3,  7,  4, 12,  5,  6, 11,  0, 14,  9,  2},
                     { 7, 11,  4,  1,  9, 12, 14,  2,  0,  6, 10, 13, 15,  3,  5,  8},
                     { 2,  1, 14,  7,  4, 10,  8, 13, 15, 12,  9,  0,  3,  5,  6, 11}};
    
    init {}
}
// Implements DES encyption algorithm
// (rodric rabbah, <rabbah@mit.edu>)

void->void pipeline DES2
{
    int testvector = 7;

    //add PlainTextSource(testvector);
    add FileReader<int>("../input/input");
    add DEScoder(testvector);
    add FileWriter<int>("des2.out");
    //add HexPrinter(CIPHERTEXT, 64);
}

int->int pipeline DEScoder(int vector)
{
    // initial permutation of 64 bit plain text
    add doIP();

    for (int i = 0; i < MAXROUNDS; i++) {
        add splitjoin {
            split duplicate;
            // R[i+1] = f(R[i]) xor L[i]
            add nextR(vector, i);
            // L[i+1] = R[i]
            add nextL();
            join roundrobin(32, 32);
        }
    }
    add CrissCross();

    add doIPm1();
}

int->int filter doIP()
{
    work push 64 pop 64 {
        for (int i = 0; i < 64; i++) {
            push(peek(IP[i] - 1));
        }
        for (int i = 0; i < 64; i++) {
            pop();
        }
    }
}

// L[i+1] is lower 32 bits of current 64 bit input
// input  is LR[i]
// output is  R[i]
int->int filter nextL()
{
    work pop 64 push 32 {
        for (int i = 0; i < 32; i++) {
            push(pop());
        }
        for (int i = 0; i < 32; i++) {
            pop(); // L[i] is decimated
        }
    }
}

// R[i+1] is f(R[i]) xor L[i]
// R[i] is lower 32 bits of input stream
// L[i] is upper 32 bits of input stream
// input is LR[i]
// output is f(R[i]) xor L[i]
int->int pipeline nextR(int vector, int round)
{
    add splitjoin {
        split roundrobin(32, 32);
        add f(vector, round);
        add Identity<int>();
        join roundrobin;
    }
    add Xor(2);
}

int->int pipeline f(int vector, int round)
{
    // expand R from 32 to 48 bits and xor with key
    // add splitjoin {
    //    split roundrobin(32, 0);
          add doE(); 
          add KeySchedule(vector, round); 
    //    join roundrobin;
    // }
    add Xor(2);

    // apply substitutions to generate 32 bit cipher
    add Sboxes();

    // permute the bits
    add doP();
}

int->int filter doE()
{
    work pop 32 push 48 {
        for (int i = 0; i < 48; i++) {
            push(peek(E[i] - 1));
        }
        for (int i = 0; i < 32; i++) {
            pop();
        }
    }
}

int->int filter doP()
{
    work pop 32 push 32 {
        // input bit stream is from MSB ... LSB 
        // that is LSB is head of FIFO, MSB is tail of FIFO
        // as in b63 b62 b61 b60 ... b3  b2  b1  b0
        // but P permutation requires bit numbering from left to right
        // as in b1  b2  b3  b4  ... b61 b62 b63 b64
        // (note indexing from 0 vs 1)
        // permutation P permutes the bits and emits them
        // in reverse order
        for (int i = 31; i >= 0; i--) {
            push(peek(32 - P[i]));
        }
        for (int i = 0; i < 32; i++) {
            pop();
        }
    }
}

int->int filter doIPm1()
{
    work push 64 pop 64 {
        for (int i = 0; i < 64; i++) {
            push(peek(IPm1[i] - 1));
        }
        for (int i = 0; i < 64; i++) {
            pop();
        }
    }
}
// output encryption key for current round; fully
// precalculates key schedules at init time, and 
// emits the appropriate bits at run time
int->int pipeline KeySchedule(int vector, int round)
{
    add int->int filter {
        int[MAXROUNDS][48] keys;

        // precalculate key schedule
        init {
            int[64] k64;
            
            for (int w = 1; w >= 0; w--) {
                int v = USERKEYS[vector][w]; // LSW first then MSW
                int m = 1;
                for (int i = 0; i < 32; i++) {
                    if (((v & m) >> i) != 0)
                        k64[((1 - w) * 32) + i] = 1;
                    else
                        k64[((1 - w) * 32) + i] = 0;
                    m = m << 1;
                }
            }

            // apply PC1
            int[56] k56;
            for (int i = 0; i < 56; i++) {
                // input bit stream is from MSB ... LSB 
                // that is LSB is head of FIFO, MSB is tail of FIFO
                // as in b63 b62 b61 b60 ... b3  b2  b1  b0
                // but PC1 permutation requires bit numbering from left to right
                // as in b1  b2  b3  b4  ... b61 b62 b63 b64
                // (note indexing from 0 vs 1)
                k56[i] = k64[64 - PC1[i]];
            }

            for (int r = 0; r < MAXROUNDS; r++) {
                // rotate left and right 28-bit bits chunks
                // according to round number
                int[56] bits;
                for (int i =  0; i < 28; i++) 
                    bits[i] = k56[(i + RT[r]) % 28];
                for (int i = 28; i < 56; i++) 
                    bits[i] = k56[28 + ((i + RT[r]) % 28)];
                for (int i =  0; i < 56; i++) 
                    k56[i] = bits[i];
                
                // apply PC2 and store resultant key
                for (int i = 47; i >= 0; i--) {
                    // input bit stream is from MSB ... LSB
                    // that is LSB is head of FIFO, MSB is tail of FIFO
                    // as in b63 b62 b61 b60 ... b3  b2  b1  b0
                    // permutation PC2 permutes the bits then emits them
                    // in reverse order
                    keys[r][47 - i] = k56[PC2[i] - 1];
                }
            }
        }

        work pop 48 push 2*48 {
            for (int i = 0; i < 48; i++) {
                push(keys[round][i]);
                push(pop());
            }
        }
    }

    if (PRINTINFO && (round == 0)) {
        add splitjoin {
            split duplicate;
            add Identity<int>();
            add pipeline {
                add int->int filter {
                    work pop 48*2 push 2 {
                        for (int i = 0; i < 48*2; i++) pop();

                        push(USERKEYS[vector][1]); // LSW
                        push(USERKEYS[vector][0]); // MSW
                    }
                }
                add IntoBits();
                add HexPrinter(USERKEY, 64);
            }
            join roundrobin(1, 0);
        }
    }
}

// inefficient but straightforward implementation of key schedule; it
// recalculates all keys for all previous rounds 1...i-1
void->int pipeline slowKeySchedule(int vector, int round)
{
    add void->int filter {
        work push 2 {
            push(USERKEYS[vector][1]); // LSW
            push(USERKEYS[vector][0]); // MSW
        }
    }

    add IntoBits();

    add doPC1();
    
    for (int i = 0; i < round+1; i++) {
        add splitjoin {
            split roundrobin(28, 28);
            add LRotate(i);
            add LRotate(i);
            join roundrobin(28, 28);
        }
        // or more simply can do:
        // add LRotate(i);
    }
    
    add doPC2();
    
    if (PRINTINFO && (round == 0)) {
        add splitjoin {
            split duplicate;
            add Identity<int>();
            add pipeline {
                add int->int filter {
                    work pop 48 push 2 {
                        for (int i = 0; i < 48; i++) pop();

                        push(USERKEYS[vector][1]); // LSW
                        push(USERKEYS[vector][0]); // MSW
                    }
                }
                add IntoBits();
                add HexPrinter(USERKEY, 64);
            }
            join roundrobin(1, 0);
        }
    }
}

// left rotate input stream of length 28-bits by RT[round]
int->int filter LRotate(int round)
{
    int n = 28;
    int x = RT[round];

    work pop n push n {
        for (int i = 0; i < n; i++) {
            push(peek((i + x) % n));
        }
        for (int i = 0; i < n; i++) {
            pop();
        }
    }
}

int->int filter doPC1()
{
    work pop 64 push 56 {
        for (int i = 0; i < 56; i++) {
            // input bit stream is from MSB ... LSB 
            // that is LSB is head of FIFO, MSB is tail of FIFO
            // as in b63 b62 b61 b60 ... b3  b2  b1  b0
            // but PC1 permutation requires bit numbering from left to right
            // as in b1  b2  b3  b4  ... b61 b62 b63 b64
            // (note indexing from 0 vs 1)
            push(peek(64 - PC1[i]));
        }
        for (int i = 0; i < 64; i++) {
            pop();
        }
    }
}
       
int->int filter doPC2()
{
    work pop 56 push 48 {
        // input bit stream is from MSB ... LSB
        // that is LSB is head of FIFO, MSB is tail of FIFO
        // as in b63 b62 b61 b60 ... b3  b2  b1  b0
        // permutation PC2 permutes the bits then emits them
        // in reverse order
        for (int i = 47; i >= 0; i--) {
            push(peek(PC2[i] - 1));
        }
        for (int i = 0; i < 56; i++) {
            pop();
        }
    }
}

int->int filter Sboxes()
{
    work pop 6 * 8 push 4 * 8 {
        for (int i = 1; i <= 8; i++) {
            int r = pop(); // r = first and last bit
            int c = pop(); // c = middle four bits
            c = (pop() << 1) | c;
            c = (pop() << 2) | c;
            c = (pop() << 3) | c;
            r = (pop() << 1) | r;
            
            int out = 0;
            if (i == 1) out = S8[r][c]; // lower 8 bits
            else if (i == 2) out = S7[r][c]; // next  8 bits
            else if (i == 3) out = S6[r][c]; // ...
            else if (i == 4) out = S5[r][c];
            else if (i == 5) out = S4[r][c];
            else if (i == 6) out = S3[r][c];
            else if (i == 7) out = S2[r][c];
            else if (i == 8) out = S1[r][c]; // last (upper) 8 bits
            
            push((int)((out & 0x1) >> 0));
            push((int)((out & 0x2) >> 1));
            push((int)((out & 0x4) >> 2));
            push((int)((out & 0x8) >> 3));
        }
    }
}

// plain text source
// input: none
// output: plain text in two words (LSW first then MSW)
void->int pipeline PlainTextSource(int vector)
{
    int[34][2] TEXT = {{0x00000000, 0x00000000},  // 0x0000000000000000
                       {0xFFFFFFFF, 0xFFFFFFFF},  // 0xFFFFFFFFFFFFFFFF
                       {0x10000000, 0x00000001},  // 0x1000000000000001
                       {0x11111111, 0x11111111},  // 0x1111111111111111
                       {0x11111111, 0x11111111},  // 0x1111111111111111
                       {0x01234567, 0x89ABCDEF},  // 0x0123456789ABCDEF
                       {0x00000000, 0x00000000},  // 0x0000000000000000
                       {0x01234567, 0x89ABCDEF},  // 0x0123456789ABCDEF
                       {0x01A1D6D0, 0x39776742},  // 0x01A1D6D039776742
                       {0x5CD54CA8, 0x3DEF57DA},  // 0x5CD54CA83DEF57DA
                       {0x0248D438, 0x06F67172},  // 0x0248D43806F67172
                       {0x51454B58, 0x2DDF440A},  // 0x51454B582DDF440A
                       {0x42FD4430, 0x59577FA2},  // 0x42FD443059577FA2
                       {0x059B5E08, 0x51CF143A},  // 0x059B5E0851CF143A
                       {0x0756D8E0, 0x774761D2},  // 0x0756D8E0774761D2
                       {0x762514B8, 0x29BF486A},  // 0x762514B829BF486A
                       {0x3BDD1190, 0x49372802},  // 0x3BDD119049372802
                       {0x26955F68, 0x35AF609A},  // 0x26955F6835AF609A
                       {0x164D5E40, 0x4F275232},  // 0x164D5E404F275232
                       {0x6B056E18, 0x759F5CCA},  // 0x6B056E18759F5CCA
                       {0x004BD6EF, 0x09176062},  // 0x004BD6EF09176062
                       {0x480D3900, 0x6EE762F2},  // 0x480D39006EE762F2
                       {0x437540C8, 0x698F3CFA},  // 0x437540C8698F3CFA
                       {0x072D43A0, 0x77075292},  // 0x072D43A077075292
                       {0x02FE5577, 0x8117F12A},  // 0x02FE55778117F12A
                       {0x1D9D5C50, 0x18F728C2},  // 0x1D9D5C5018F728C2
                       {0x30553228, 0x6D6F295A},  // 0x305532286D6F295A
                       {0x01234567, 0x89ABCDEF},  // 0x0123456789ABCDEF
                       {0x01234567, 0x89ABCDEF},  // 0x0123456789ABCDEF
                       {0x01234567, 0x89ABCDEF},  // 0x0123456789ABCDEF
                       {0xFFFFFFFF, 0xFFFFFFFF},  // 0xFFFFFFFFFFFFFFFF
                       {0x00000000, 0x00000000},  // 0x0000000000000000
                       {0x00000000, 0x00000000},  // 0x0000000000000000
                       {0xFFFFFFFF, 0xFFFFFFFF}}; // 0xFFFFFFFFFFFFFFFF

    add void->int filter {
        work push 2 {
            push(TEXT[vector][1]); // LSW
            push(TEXT[vector][0]); // MSW
        }
    }
    add IntoBits(); 

    if (PRINTINFO) {
        add splitjoin {
            split duplicate;
            add Identity<int>();
            add HexPrinter(PLAINTEXT, 64);
            join roundrobin(1, 0);
        }
    }
}

// take N streams and Xor them together
// the streams are assumed to be interleaved
int->int filter Xor(int n)
{
    work push 1 pop n {
        int x = pop();
        for (int i = 1; i < n; i++) {
            int y = pop();
            x = x ^ y;
        }
		push(x);
    }
}

// swap two input streams each of 32 bits
int->int filter CrissCross()
{
    work pop 2 * 32 push 2 * 32 {
        for (int i = 0; i < 32; i++) {
                push(peek(32 + i));
        }
        for (int i = 0; i < 32; i++) {
            push(pop());
        }
        for (int i = 0; i < 32; i++) {
            pop();
        }
    }
}

// input:  integer
// output: LSB first ... MSB last
int->int filter IntoBits() 
{
  work pop 1 push 32 {
      int v = pop();
      int m = 1;

      for (int i = 0; i < 32; i++) {
          if (((v & m) >> i) != 0)
              push(1);
          else
              push(0);
          m = m << 1;
      }
  }
}

// input:  LSB first ... MSB last
// output: integer
int->int filter BitstoInts(int n) 
{
    work peek n pop n push 1 {
        int v = 0;
        for (int i = 0; i < n; i++) {
            v = v | (pop() << i);
        }
        push(v);
    }
}

// input:  w words x b bits/word
// output: bit i from all w words, followed by i+1 for all b bits
int->int splitjoin BitSlice(int w, int b)
{
    split roundrobin(1);
    for (int l = 0; l < b; l++) {
        add Identity<int>;
    }
    join roundrobin(w);
}

// input:  LSB first ... MSB last
// output: none
// prints: MSW first ... LSW last
int->void pipeline HexPrinter(int descriptor, int n)
{
    int bits  = n;
    int bytes = bits / 4;
    
    add BitstoInts(4);
    add int->void filter {
        work pop bytes {
            if (PRINTINFO) {
                if (descriptor == PLAINTEXT) 
                    print("P: ");
                else if (descriptor == USERKEY)
                    print("K: ");
                else if (descriptor == CIPHERTEXT)
                    print("C: ");
            }

            for (int i = bytes - 1; i >= 0; i--) {
                int v = peek(i);
                if (v < 10) print(v);
                else if (v == 10) print("A");
                else if (v == 11) print("B");
                else if (v == 12) print("C");
                else if (v == 13) print("D");
                else if (v == 14) print("E");
                else if (v == 15) print("F");
                else {
                    print("ERROR: "); 
                    println(v);
                }
            }
            println("");

            for (int i = 0; i < bytes; i++) 
                pop();
        }
    }
}

// input:  LSB first ... MSB last
// output: LSB first ... MSB last (Identity)
// prints: MSW first ... LSW last (HEX format)
int->int splitjoin ShowIntermediate(int n)
{
    int bits  = n;
    int bytes = bits / 4;

    split duplicate;
    add Identity<int>;
    add pipeline {
        add BitstoInts(4);
        add int->void filter {
            work pop bytes {
                for (int i = bytes - 1; i >= 0; i--) {
                    int v = peek(i);
                    if (v < 10) print(v);
                    else if (v == 10) print("A");
                    else if (v == 11) print("B");
                    else if (v == 12) print("C");
                    else if (v == 13) print("D");
                    else if (v == 14) print("E");
                    else if (v == 15) print("F");
                    else {
                        print("ERROR: "); 
                        println(v);
                    }
                }
                println("");
                
                for (int i = 0; i < bytes; i++) 
                    pop();
            }
        }
    }
    join roundrobin(1, 0);
}

// input:  LSB first ... MSB last
// output: LSB first ... MSB last (Identity)
// prints: MSB first ... LSB last (BINARY format)
int->int filter ShowBitStream(int n, int w)
{
    work pop n push n {
        for (int i = n-1; i >= 0; i--) {
            print(peek(i));
            if ((i % w) == 0) print(" ");
        }
        println("");
        for (int i = 0; i < n; i++) 
            push(pop());
    }
}
